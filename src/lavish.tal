( lavish.tal )
|0e @System/debug  $1
|10 @Console/vector $2 &read $1 &pad $5 &write $1 &error $1

( thrift instructions )
%:quot { 00 }
%:unqt { 01 }

%:console/write ( -- ) { :quot 18 }
%:system/debug  ( -- ) { :quot 0e }
%:emit ( :byte -- ) { :console/write :quot DEO :unqt }
%:dbg  ( -- ) { :quot 01 :system/debug :quot DEO :unqt }
%:newline ( -- ) { :quot 0a :emit }

%DBG ( -- ) { [ LIT2 01 -System/debug ] DEO }

|100
@on-reset ( -> ) ;on-console .Console/vector DEO2 !:on-reset
@on-console ( -> ) .Console/read DEI BRK

( remote pseudo opcodes )
@:OPC ( op -- )
    ,/op STR
    ;/start ;/end !:asm/send-blk
    &start [
        :quot &op $1 :unqt
    ] &end

@:BRK  [ LIT BRK ]  !:OPC
@:INC  [ LIT INC ]  !:OPC
@:POP  [ LIT POP ]  !:OPC
@:NIP  [ LIT NIP ]  !:OPC
@:SWP  [ LIT SWP ]  !:OPC
@:ROT  [ LIT ROT ]  !:OPC
@:DUP  [ LIT DUP ]  !:OPC
@:OVR  [ LIT OVR ]  !:OPC

@:LIT ( -- :byte )
    STH2kr LDA INC2r
    ,/byte STR
    ;/command #0002 !:asm/send-len
    &command [ :quot &byte $1 ]

@:LIT2 ( -- :short )
    STH2kr LDA2 INC2r INC2r
    ,/lo-byte STR ,/hi-byte STR
    ;/command #0004 !:asm/send-len
    &command [ :quot &hi-byte $1 :quot &lo-byte $1 ]

( local library )
@EMIT ( byte -- :byte ) .Console/write DEO JMP2r

( remote library )
@:EMIT ( :byte -- byte ) [ :LIT -Console/write ] :DEO JMP2r
@:DBG ( -- ) [ :LIT2 01 -System/debug ] :DEO JMP2r

@:asm/reset ( -- ) #0120 ( >> )
@:asm/set ( addr* -- ) ,/pointer STR2 JMP2r
@:asm/get ( -- addr* ) ,/pointer LDA2 JMP2r
@:asm/write ( byte -- )
    [ LIT2 &pointer 0120 ] INC2k ,/pointer STR2
    ,/lo-byte STR ,/hi-byte STR ,/byte STR
    ;/start ;/end !/send-blk
    &start [
        :quot &byte $1 :quot &hi-byte $1 :quot &lo-byte $1 :quot STA :unqt
    ] &end

@:asm/send-len ( start* len* -- ) ADD2k NIP2 ( >> )
@:asm/send-blk ( start* end* -- ) ;EMIT !/for-each
@:asm/cpy-len ( start* len* -- ) ADD2k NIP2 ( >> )
@:asm/cpy-blk ( start* end* -- ) ;/write ( >> )
@:asm/for-each ( start* end* fn* -- )
    STH2 SWP2
    &loop
        LDAk STH2kr JSR2
        INC2 GTH2k ?/loop
    POP2 POP2 POP2r
    JMP2r
     
~main.tal
