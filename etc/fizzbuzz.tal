( Print the first 30 numbers using the following rules:
	| If n is divisible by 3, print "fizz"
	| If n is divisible by 5, print "buzz"
	| If n is divisible by both, print "fizzbuzz"
	| Else, print the number )

|19 @Console/error $1

|100 @fizzbuzz ( -> )
    #6501
    &>loop ( length i -- )
        DUP fizz OVR buzz ORA ?{ DUP <dec> }
        [ LIT2 0a -Console/error ] DEO
        INC GTHk ?&>loop
    POP2 BRK

%MOD ( a b -- a%b ) { DIVk MUL SUB }
%ERR ( a -- ) { .Console/error DEO }
@fizz ( n -- ) #03 MOD ?{ #01 ;Dict/fizz !<str> } #00 JMP2r
@buzz ( n -- ) #05 MOD ?{ #01 ;Dict/buzz !<str> } #00 JMP2r
@<dec> ( n -- ) DUP #0a DIV /d #0a MOD &d #30 ADD ERR JMP2r
@<str> ( s* -- ) LDAk ERR INC2 LDAk ?<str> POP2 JMP2r

@Dict &fizz "Fizz $1 &buzz "Buzz $1

